! (C) Copyright 1988- ECMWF.
!
! This software is licensed under the terms of the Apache Licence Version 2.0
! which can be obtained at http://www.apache.org/licenses/LICENSE-2.0.
!
! In applying this licence, ECMWF does not waive the privileges and immunities
! granted to it by virtue of its status as an intergovernmental organisation
! nor does it submit to any jurisdiction.

MODULE CLOUDSC_GLOBAL_ATLAS_STATE_MOD
  ! Driver module to manage the setup and teardown of the dwarf memory state
  USE PARKIND1,  ONLY : JPIM, JPRB
  USE YOMPHYDER, ONLY : STATE_TYPE
  USE YOECLDP,   ONLY : NCLV, YRECLDP, YRECLDP_LOAD_PARAMETERS
  USE YOMCST,    ONLY : YOMCST_LOAD_PARAMETERS
  USE YOETHF,    ONLY : YOETHF_LOAD_PARAMETERS
  USE YOEPHLI  , ONLY : YREPHLI, YREPHLI_LOAD_PARAMETERS

  USE FILE_IO_MOD, ONLY: INPUT_INITIALIZE, INPUT_FINALIZE, LOAD_SCALAR, LOAD_ARRAY
  USE EXPAND_ATLAS_MOD, ONLY: LOADVAR_ATLAS, LOADSTATE_ATLAS
  USE VALIDATE_ATLAS_MOD, ONLY: VALIDATEVAR_ATLAS, VALIDATESTATE_ATLAS
  USE CLOUDSC_MPI_MOD, ONLY: IRANK

  USE ATLAS_MODULE
  USE, INTRINSIC :: ISO_C_BINDING
  USE ATLAS_FUNCTIONSPACE_BLOCKSTRUCTUREDCOLUMNS_MODULE
  USE ATLAS_MULTIFIELD_MODULE

  IMPLICIT NONE

  TYPE VAR3D_PTR
      REAL(C_DOUBLE), POINTER :: PTR(:,:,:)
  END TYPE
  TYPE VAR2D_PTR
      REAL(C_DOUBLE), POINTER :: PTR(:,:)
  END TYPE

  CHARACTER(LEN=10), PARAMETER, DIMENSION(30) :: IN_VAR_NAMES = (/ &
      "PLCRIT_AER", "PICRIT_AER", "PRE_ICE   ", "PCCN      ", "PNICE     ", "PT        ", "PQ        ", &
      "PVFA      ", "PVFL      ", "PVFI      ", "PDYNA     ", "PDYNL     ", "PDYNI     ", "PHRSW     ", &
      "PHRLW     ", "PVERVEL   ", "PAP       ", "PLU       ", "PLUDE     ", "PSNDE     ", "PMFU      ", &
      "PMFD      ", "PA        ", "PSUPSAT   ", &
      "PLSM      ", "LDCUM     ", "KTYPE     ", "PAPH      ", "PEXTRA    ", "PCLV      " /)
  CHARACTER(LEN=16), PARAMETER, DIMENSION(16) :: OUT_VAR_NAMES = (/ &
      "PFSQLF          ", "PFSQIF          ", "PFCQLNG         ", "PFCQNNG         ", "PFSQRF          ", &
      "PFSQSF          ", "PFCQRNG         ", "PFCQSNG         ", "PFSQLTUR        ", "PFSQITUR        ", &
      "PFPLSL          ", "PFPLSN          ", "PFHPSL          ", "PFHPSN          ", "PCOVPTOT        ", &
      "PRAINFRAC_TOPRFZ" /) 

  TYPE CLOUDSC_GLOBAL_ATLAS_STATE_BLOCK_VIEW
    TYPE(VAR2D_PTR), DIMENSION(24) :: IN_VARS_2D_REAL64
    TYPE(VAR2D_PTR), DIMENSION(15) :: OUT_VARS_2D_REAL64

    ! Input field variables and tendencies
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PLCRIT_AER(:,:)
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PICRIT_AER(:,:) 
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PRE_ICE(:,:) 
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PCCN(:,:)     ! liquid cloud condensation nuclei
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PNICE(:,:)    ! ice number concentration (cf. CCN)
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PT(:,:)       ! T at start of callpar
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PQ(:,:)       ! Q at start of callpar
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PVFA(:,:)     ! CC from VDF scheme
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PVFL(:,:)     ! Liq from VDF scheme
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PVFI(:,:)     ! Ice from VDF scheme
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PDYNA(:,:)    ! CC from Dynamics
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PDYNL(:,:)    ! Liq from Dynamics
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PDYNI(:,:)    ! Liq from Dynamics
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PHRSW(:,:)    ! Short-wave heating rate
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PHRLW(:,:)    ! Long-wave heating rate
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PVERVEL(:,:)  ! Vertical velocity
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PAP(:,:)      ! Pressure on full levels
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PLU(:,:)      ! Conv. condensate
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PLUDE(:,:)    ! Conv. detrained water 
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PSNDE(:,:)    ! Conv. detrained snow
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PMFU(:,:)     ! Conv. mass flux up
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PMFD(:,:)     ! Conv. mass flux down
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PA(:,:)       ! Original Cloud fraction (t)
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PSUPSAT(:,:)

    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PLSM(:)       ! Land fraction (0-1) 
    LOGICAL,        POINTER, CONTIGUOUS :: LDCUM(:)      ! Convection active
    INTEGER,        POINTER, CONTIGUOUS :: KTYPE(:)      ! Convection type 0,1,2
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PAPH(:,:)     ! Pressure on half levels
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PEXTRA(:,:,:) ! extra fields
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PCLV(:,:,:) 

    TYPE(STATE_TYPE) :: TENDENCY_CML ! cumulative tendency used for final output
    TYPE(STATE_TYPE) :: TENDENCY_TMP ! cumulative tendency used as input
    TYPE(STATE_TYPE) :: TENDENCY_LOC ! local tendency from cloud scheme

    ! Output fields used for validation
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PFSQLF(:,:)   ! Flux of liquid
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PFSQIF(:,:)   ! Flux of ice
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PFCQLNG(:,:)  ! -ve corr for liq
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PFCQNNG(:,:)  ! -ve corr for ice
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PFSQRF(:,:)   ! Flux diagnostics
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PFSQSF(:,:)   ! for DDH, generic
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PFCQRNG(:,:)  ! rain
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PFCQSNG(:,:)  ! snow
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PFSQLTUR(:,:) ! liquid flux due to VDF
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PFSQITUR(:,:) ! ice flux due to VDF
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PFPLSL(:,:)   ! liq+rain sedim flux
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PFPLSN(:,:)   ! ice+snow sedim flux
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PFHPSL(:,:)   ! Enthalpy flux for liq
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PFHPSN(:,:)   ! Enthalpy flux for ice
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PCOVPTOT(:,:) ! Precip fraction
    REAL(C_DOUBLE), POINTER, CONTIGUOUS :: PRAINFRAC_TOPRFZ(:) 

    CONTAINS
    PROCEDURE :: GET_BLOCK => CLOUDSC_GLOBAL_ATLAS_STATE_BLOCK
  END TYPE CLOUDSC_GLOBAL_ATLAS_STATE_BLOCK_VIEW

  TYPE CLOUDSC_GLOBAL_ATLAS_STATE
    ! Memory state containing raw fields annd tendencies for CLOUDSC dwarf
    !
    ! Note that the global state has an additional outermost block
    ! dimension allocated for each field variable.
    INTEGER(KIND=JPIM)                   :: NPROMA, KLEV    ! Grid points and vertical levels per block
    INTEGER(KIND=JPIM)                   :: NGPTOT, NBLOCKS ! Total number of grid points and blocks
    INTEGER(KIND=JPIM)                   :: KFLDX 
    LOGICAL                              :: LDSLPHY 
    LOGICAL                              :: LDMAINCALL      ! T if main call to cloudsc
    REAL(KIND=JPRB)                      :: PTSPHY          ! Physics timestep

  CONTAINS
    PROCEDURE :: LOAD => CLOUDSC_GLOBAL_ATLAS_STATE_LOAD
    PROCEDURE :: VALIDATE => CLOUDSC_GLOBAL_ATLAS_STATE_VALIDATE
  END TYPE CLOUDSC_GLOBAL_ATLAS_STATE

CONTAINS

  SUBROUTINE CLOUDSC_GLOBAL_ATLAS_STATE_BLOCK(SELF, FSET, IBLK)
    CLASS(CLOUDSC_GLOBAL_ATLAS_STATE_BLOCK_VIEW), INTENT(INOUT) :: SELF
    CLASS(ATLAS_FIELDSET), INTENT(INOUT) :: FSET
    INTEGER, INTENT(IN) :: IBLK

    REAL(C_DOUBLE), POINTER :: TMP3D(:,:,:)

!    CALL FSET%UPDATE_DEVICE(1,IBLK)
!    field = fset%field(1)
!    call field%update_device()

    CALL FSET%DATA(1, SELF%PLCRIT_AER, IBLK)
    CALL FSET%DATA(2, SELF%PICRIT_AER, IBLK)
    CALL FSET%DATA(3, SELF%PRE_ICE, IBLK)
    CALL FSET%DATA(4, SELF%PCCN, IBLK)
    CALL FSET%DATA(5, SELF%PNICE, IBLK)
    CALL FSET%DATA(6, SELF%PT, IBLK)
    CALL FSET%DATA(7, SELF%PQ, IBLK)
    CALL FSET%DATA(8, SELF%PVFA, IBLK)
    CALL FSET%DATA(9, SELF%PVFL, IBLK)
    CALL FSET%DATA(10, SELF%PVFI, IBLK)
    CALL FSET%DATA(11, SELF%PDYNA, IBLK)
    CALL FSET%DATA(12, SELF%PDYNL, IBLK)
    CALL FSET%DATA(13, SELF%PDYNI, IBLK)
    CALL FSET%DATA(14, SELF%PHRSW, IBLK)
    CALL FSET%DATA(15, SELF%PHRLW, IBLK)
    CALL FSET%DATA(16, SELF%PVERVEL, IBLK)
    CALL FSET%DATA(17, SELF%PAP, IBLK)
    CALL FSET%DATA(18, SELF%PLU, IBLK)
    CALL FSET%DATA(19, SELF%PLUDE, IBLK)
    CALL FSET%DATA(20, SELF%PSNDE, IBLK)
    CALL FSET%DATA(21, SELF%PMFU, IBLK)
    CALL FSET%DATA(22, SELF%PMFD, IBLK)
    CALL FSET%DATA(23, SELF%PA, IBLK)
    CALL FSET%DATA(24, SELF%PSUPSAT, IBLK)
    CALL FSET%DATA(25, SELF%PLSM, IBLK)
    CALL FSET%DATA(26, SELF%LDCUM, IBLK)
    CALL FSET%DATA(27, SELF%KTYPE, IBLK)
    CALL FSET%DATA(28, SELF%PAPH, IBLK)
    CALL FSET%DATA(29, SELF%PEXTRA, IBLK)
    CALL FSET%DATA(30, SELF%PCLV, IBLK)

    CALL FSET%DATA(31, TMP3D, IBLK)
    SELF%TENDENCY_CML%T   => TMP3D(:,:,1)
    SELF%TENDENCY_CML%A   => TMP3D(:,:,2)
    SELF%TENDENCY_CML%Q   => TMP3D(:,:,3)
    SELF%TENDENCY_CML%CLD => TMP3D(:,:,4:)
    CALL FSET%DATA(32, TMP3D, IBLK)
    SELF%TENDENCY_TMP%T   => TMP3D(:,:,1)
    SELF%TENDENCY_TMP%A   => TMP3D(:,:,2)
    SELF%TENDENCY_TMP%Q   => TMP3D(:,:,3)
    SELF%TENDENCY_TMP%CLD => TMP3D(:,:,4:)
    CALL FSET%DATA(33, TMP3D, IBLK)
    SELF%TENDENCY_LOC%T   => TMP3D(:,:,1)
    SELF%TENDENCY_LOC%A   => TMP3D(:,:,2)
    SELF%TENDENCY_LOC%Q   => TMP3D(:,:,3)
    SELF%TENDENCY_LOC%CLD => TMP3D(:,:,4:)

    CALL FSET%DATA(34, SELF%PFSQLF, IBLK)
    CALL FSET%DATA(35, SELF%PFSQIF, IBLK)
    CALL FSET%DATA(36, SELF%PFCQLNG, IBLK)
    CALL FSET%DATA(37, SELF%PFCQNNG, IBLK)
    CALL FSET%DATA(38, SELF%PFSQRF, IBLK)
    CALL FSET%DATA(39, SELF%PFSQSF, IBLK)
    CALL FSET%DATA(40, SELF%PFCQRNG, IBLK)
    CALL FSET%DATA(41, SELF%PFCQSNG, IBLK)
    CALL FSET%DATA(42, SELF%PFSQLTUR, IBLK)
    CALL FSET%DATA(43, SELF%PFSQITUR, IBLK)
    CALL FSET%DATA(44, SELF%PFPLSL, IBLK)
    CALL FSET%DATA(45, SELF%PFPLSN, IBLK)
    CALL FSET%DATA(46, SELF%PFHPSL, IBLK)
    CALL FSET%DATA(47, SELF%PFHPSN, IBLK)
    CALL FSET%DATA(48, SELF%PCOVPTOT, IBLK)
    CALL FSET%DATA(49, SELF%PRAINFRAC_TOPRFZ, IBLK)
  END SUBROUTINE CLOUDSC_GLOBAL_ATLAS_STATE_BLOCK

  SUBROUTINE CLOUDSC_GLOBAL_ATLAS_STATE_LOAD(SELF, FSET, MULTIFIELD_IN, MULTIFIELD_OUT, FSPACE, NPROMA, NGPTOTG)
    ! Load reference input data via serialbox
    CLASS(CLOUDSC_GLOBAL_ATLAS_STATE) :: SELF
    TYPE(ATLAS_FIELDSET), INTENT(INOUT) :: FSET
    TYPE(ATLAS_FUNCTIONSPACE_BLOCKSTRUCTUREDCOLUMNS), INTENT(INOUT) :: FSPACE
    TYPE(ATLAS_MULTIFIELD), INTENT(INOUT) :: MULTIFIELD_IN, MULTIFIELD_OUT
    INTEGER(KIND=JPIM), INTENT(IN) :: NPROMA, NGPTOTG

    TYPE(ATLAS_STRUCTUREDGRID) :: GRID
    INTEGER(KIND=JPIM) :: KLON, IVAR, B
    TYPE(VAR3D_PTR), DIMENSION(24) :: IN_VARS_3D_REAL64
    TYPE(VAR3D_PTR), DIMENSION(15) :: OUT_VARS_3D_REAL64
    REAL(C_DOUBLE), POINTER :: TMP3D(:,:,:)
    REAL(C_DOUBLE), POINTER :: TMP2D(:,:)
    TYPE(ATLAS_FIELD) :: FIELD
    TYPE(ATLAS_CONFIG) :: CONFIG
    TYPE(ATLAS_CONFIG), DIMENSION(24) :: IN_MFIELD_CONFIG
    TYPE(ATLAS_CONFIG), DIMENSION(14) :: OUT_MFIELD_CONFIG
    INTEGER :: INVAR_SIZE, OUTVAR_SIZE

    CALL INPUT_INITIALIZE(NAME='input')

    CALL LOAD_SCALAR('KLON', KLON)
    CALL LOAD_SCALAR('KLEV', SELF%KLEV)
    CALL LOAD_SCALAR('KFLDX', SELF%KFLDX)

    GRID = ATLAS_REGULARLONLATGRID(NGPTOTG, 1)
    FSPACE = ATLAS_FUNCTIONSPACE_BLOCKSTRUCTUREDCOLUMNS(GRID, LEVELS=SELF%KLEV, NPROMA=NPROMA, HALO=0)
    SELF%NBLOCKS = FSPACE%NBLKS()

    ! create multifield
    CONFIG = atlas_CONFIG()
    CALL CONFIG%SET("type", "MultiFieldCreatorIFS")
    CALL CONFIG%SET("nproma", NPROMA)
    CALL CONFIG%SET("ngptot", FSPACE%SIZE())
    CALL CONFIG%SET("datatype", "real64")
    
    ! input multifield on model levels, i.e. LEVELS = FSPACE%LEVELS() = SELF%KLEV
    INVAR_SIZE = SIZE(IN_VAR_NAMES) - 6 ! the last six variables are special and added through FieldSet
    DO IVAR = 1, INVAR_SIZE
        IN_MFIELD_CONFIG(IVAR) = ATLAS_CONFIG()
        CALL IN_MFIELD_CONFIG(IVAR)%SET("name", TRIM(IN_VAR_NAMES(IVAR)))
    END DO
    CALL CONFIG%SET("nlev", FSPACE%LEVELS())
    CALL CONFIG%SET("fields", IN_MFIELD_CONFIG)
    MULTIFIELD_IN = atlas_MultiField(CONFIG)
    CALL FSET%ADD(MULTIFIELD_IN%FIELDSET()) 

    ! special six input variables
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME="PLSM",   KIND=ATLAS_REAL(JPRB),    LEVELS=0))
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME="LDCUM",  KIND=ATLAS_LOGICAL(),     LEVELS=0))
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME="KTYPE",  KIND=ATLAS_INTEGER(JPIM), LEVELS=0))
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME="PAPH",   KIND=ATLAS_REAL(JPRB),    LEVELS=SELF%KLEV+1))
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME="PEXTRA", KIND=ATLAS_REAL(JPRB),    VARIABLES=MAX(1,SELF%KFLDX)))
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME="PCLV",   KIND=ATLAS_REAL(JPRB),    VARIABLES=MAX(1,NCLV)))
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME='TENDENCY_CML', KIND=ATLAS_REAL(JPRB), VARIABLES=3+NCLV))
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME='TENDENCY_TMP', KIND=ATLAS_REAL(JPRB), VARIABLES=3+NCLV))
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME='TENDENCY_LOC', KIND=ATLAS_REAL(JPRB), VARIABLES=3+NCLV))

    ! output multifield on model interfaces, i.e. LEVELS = FSPACE%LEVELS() + 1 = SELF%KLEV + 1
    OUTVAR_SIZE = SIZE(OUT_VAR_NAMES) - 2 ! the last two variables are special and added through FieldSet
    DO IVAR = 1, OUTVAR_SIZE
        OUT_MFIELD_CONFIG(IVAR) = ATLAS_CONFIG()
        CALL OUT_MFIELD_CONFIG(IVAR)%SET("name", TRIM(OUT_VAR_NAMES(IVAR)))
        CALL OUT_MFIELD_CONFIG(IVAR)%SET("levels", SELF%KLEV+1)
    END DO
    CALL CONFIG%SET("nlev", 1 + FSPACE%LEVELS())
    CALL CONFIG%SET("fields", OUT_MFIELD_CONFIG)
    MULTIFIELD_OUT = atlas_MultiField(CONFIG)
    CALL FSET%ADD(MULTIFIELD_OUT%FIELDSET()) 

    ! special two output variables
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME="PCOVPTOT", KIND=ATLAS_REAL(JPRB)))
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME="PRAINFRAC_TOPRFZ", KIND=ATLAS_REAL(JPRB), LEVELS=0))

    DO IVAR = 1, SIZE(IN_VAR_NAMES)
        CALL LOADVAR_ATLAS(FSET, FSPACE, TRIM(IN_VAR_NAMES(IVAR)), KLON, NGPTOTG)
    ENDDO
    CALL LOADSTATE_ATLAS(FSET, 'TENDENCY_CML', KLON, NGPTOTG)
    CALL LOADSTATE_ATLAS(FSET, 'TENDENCY_TMP', KLON, NGPTOTG)

    ! Output fields are simply zero'd
    DO IVAR = 1, SIZE(OUT_VAR_NAMES) - 1
        FIELD = FSET%FIELD(TRIM(OUT_VAR_NAMES(IVAR)))
        CALL FIELD%DATA(TMP3D)
        !$OMP PARALLEL DO DEFAULT(SHARED), PRIVATE(B) schedule(runtime)
        DO B=1, SELF%NBLOCKS
           TMP3D(:,:,B) = 0.0_JPRB
        END DO
        !$omp end parallel do
    ENDDO

    FIELD = FSET%FIELD("PRAINFRAC_TOPRFZ")
    CALL FIELD%DATA(TMP2D)
    !$OMP PARALLEL DO DEFAULT(SHARED), PRIVATE(B) schedule(runtime)
    DO B=1, SELF%NBLOCKS
       TMP2D(:,B) = 0.0_JPRB
    END DO
    !$OMP END PARALLEL DO

    ! Initialize global parameters from the input file
    CALL LOAD_SCALAR('PTSPHY', SELF%PTSPHY)
    CALL LOAD_SCALAR('LDSLPHY', SELF%LDSLPHY)
    CALL LOAD_SCALAR('LDMAINCALL', SELF%LDMAINCALL)
    CALL YOMCST_LOAD_PARAMETERS()
    CALL YOETHF_LOAD_PARAMETERS()
    CALL YRECLDP_LOAD_PARAMETERS()
    CALL YREPHLI_LOAD_PARAMETERS()

    CALL INPUT_FINALIZE()
  END SUBROUTINE CLOUDSC_GLOBAL_ATLAS_STATE_LOAD

  SUBROUTINE CLOUDSC_GLOBAL_ATLAS_STATE_VALIDATE(SELF, FSET, FSPACE, NGPTOTG)
    ! Validate the correctness of output against reference data
    CLASS(CLOUDSC_GLOBAL_ATLAS_STATE) :: SELF
    TYPE(ATLAS_FIELDSET), INTENT(INOUT) :: FSET
    TYPE(ATLAS_FUNCTIONSPACE_BLOCKSTRUCTUREDCOLUMNS), INTENT(IN) :: FSPACE
    INTEGER(KIND=JPIM), INTENT(IN) :: NGPTOTG

    INTEGER(KIND=JPIM) :: KLON, IVAR

    CALL INPUT_INITIALIZE(NAME='reference')
    CALL LOAD_SCALAR('KLON', KLON)
    print *, "KLON = ", KLON
    CALL INPUT_FINALIZE()

    ! Write variable validation header
    IF (IRANK == 0) THEN
      print '(1X,A20,1X,A3,5(1X,A20))', &
           & 'Variable','Dim', 'MinValue','MaxValue','AbsMaxErr','AvgAbsErr/GP','MaxRelErr-%'
    END IF


    ! Actual variable validation
    CALL VALIDATEVAR_ATLAS(FSET, FSPACE, 'PLUDE', KLON, NGPTOTG)
    DO IVAR = 1, SIZE(OUT_VAR_NAMES)
        CALL VALIDATEVAR_ATLAS(FSET, FSPACE, TRIM(OUT_VAR_NAMES(IVAR)), KLON, NGPTOTG)
    ENDDO
    CALL VALIDATESTATE_ATLAS(FSET, FSPACE, 'TENDENCY_LOC', KLON, NGPTOTG)

  END SUBROUTINE CLOUDSC_GLOBAL_ATLAS_STATE_VALIDATE

END MODULE CLOUDSC_GLOBAL_ATLAS_STATE_MOD
